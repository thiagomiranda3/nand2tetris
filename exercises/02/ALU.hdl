/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Handle zx
    And16(a=x, b=false, out=zeroX);
    Mux16(a=x, b=zeroX, sel=zx, out=outZX);
    
    // Handle zy
    And16(a=y, b=false, out=zeroY);
    Mux16(a=y, b=zeroY, sel=zy, out=outZY);
    
    // Handle nx
    Not16(in=outZX, out=notNX);
    Mux16(a=outZX, b=notNX, sel=nx, out=outNX);
    
    // Handle ny
    Not16(in=outZY, out=notNY);
    Mux16(a=outZY, b=notNY, sel=ny, out=outNY);
    
    // Handle f
    Add16(a=outNX, b=outNY, out=outAddXY);
    And16(a=outNX, b=outNY, out=outAndXY);
    Mux16(a=outAndXY, b=outAddXY, sel=f, out=outF);
    
    // Handle no
    Not16(in=outF, out=outNotF);
    Mux16(a=outF, b=outNotF, sel=no, out=res);
    
    // Handle control flag ng
    And16(a=res, b[15]=true, out[15]=ng);

    // Handle control flag zr
    Or16Way(in=res, out=zrOut);
    Not(in=zrOut, out=zr);

    Or16(a=res, b=false, out=out);
}